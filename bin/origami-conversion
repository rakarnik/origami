#!/usr/bin/perl

use strict;
use Switch;
use POSIX qw(floor);
use Getopt::Long;


my $showhelp = '';
my $center = 0;
my $bedmult = 1000;
my $bedname = "origami";
my $beddesc = "origami";

GetOptions("help|h"=>\$showhelp,
            "center|c:100"=>\$center,
            "bedmultiplier=i"=>\$bedmult,
            "bedname|n=s"=>\$bedname,
            "beddesc|d=s"=>\$beddesc)
or die "Error in parsing arguments";


sub helpmenu {
  print "origami-conversion [options] <bed/bedpe/washu> <results file> <score column>\n";
  print "\t--help|-h\tHelp menu (this menu)\n";
  print "\t--center|-c [int]\tPrint anchors as INT base pairs around center of anchor\n";
  print "\t--bedmultiplier [int]\tMultiply origami value by INT for BED score column\n";
  print "\t--bedname|-n [string]\tSets the BED track name field (default: origami)\n";
  print "\t--beddesc|-d [string]\tSets the BED description field (default: origami)\n";
}

sub tobed {
  my $c = $_[0];
  my $i = 1;
  
  die "Bed multiplier must be an integer greater than 0!" if $bedmult <= 0;
  
  print "track name=\"$bedname\" description=\"$beddesc\"\n";
  while(<F>) {
    chomp;
    my @arr = split /,/;
    
    next unless $arr[0] eq $arr[3]; # only print lines with intrachromosomal interactions (since BED is not designed to handle interchromsomal data)
    
    my $val = floor($arr[5+$c]*$bedmult);
    my $size = ($arr[2]-$arr[1]) . ',' . ($arr[5]-$arr[4]);
    my $starts = ($arr[1]-$arr[1]) . ',' . ($arr[4]-$arr[1]);
    
    print "$arr[0]\t$arr[1]\t$arr[5]\tInteraction$i\t$val\t+\t$arr[1]\t$arr[1]\t0,0,0\t2\t$size\t$starts\n";
    $i++;
  }
}

sub tobedpe {
  my $c = $_[0];
  while(<F>) {
    chomp;
    my @arr = split/,/;
  
    print "$arr[0]\t$arr[1]\t$arr[2]\t$arr[3]\t$arr[4]\t$arr[5]\tInteraction\t$arr[5+$c]\t.\t.\n";
  }
}

sub towashu {
  my $c = $_[0];
  while(<F>) {
    chomp;
    my @arr = split /,/;
    my ($chrom1,$start1,$end1,$chrom2,$start2,$end2) = @arr;
  
    next unless $arr[0] eq $arr[3]; # Remove interchromosomal interactions
    next if $arr[0] =~ /chrM/; # the browser doesn't like chrM
    
    if($center>0) {
      my $mid1 = ($end1-$start1)/2;
      my $mid2 = ($end2-$start2)/2;
      
      $center++ if( ($center%2) != 0 );
      
      my $half = $center/2;
      
      $end1 = $start1+$mid1+$half;
      $start1 = $start1+$mid1-$half;
      $end2 = $start2+$mid2+$half;
      $start2 = $start2+$mid2-$half;
    }
  
    print "$chrom1:$start1-$end1\t$chrom2:$start2-$end2\t$arr[5+$c]\n";
  }
}

if( $showhelp eq 1 ) {
  helpmenu();
  exit 0;
}

if( $#ARGV < 2 ) {
  helpmenu("Not enough arguments passed");
  exit 1;
}



my $option = $ARGV[0];
my $file = $ARGV[1];
my $column = $ARGV[2];

if( $file eq '-' ) {
  $file = "<&STDIN";
} elsif( ! -e $file ) {
  die "$file does not exist!";
}

die "Score column is not a number" unless $column =~ /\d+/;

open(F,"$file") or die "Cannot open $!";
<F>; # discard header line

switch($option) {
  case "bed"    { tobed($column); }
  case "bedpe"  { tobedpe($column); }
  case "washu"  { towashu($column); }
  else          { print "Invalid option: $option\n"; }
}
close(F);